group 'paireportsclient'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile 'org.apache.httpcomponents:httpclient:4.5.9'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        shadow(MavenPublication) {
            publication ->
                project.shadow.component(publication)
                customizePom(pom)
                groupId 'com.gopai'
                artifactId 'paireportsclient'
                version '1.0.1'

                from components.java

                // create the sign pom artifact
                pom.withXml {
                    def pomFile = file("${project.buildDir}/generated-pom.xml")
                    writeTo(pomFile)
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }

                artifact(sourceJar) {
                    classifier = 'sources'
                }
                artifact(javadocJar) {
                    classifier = 'javadoc'
                }

                // create the signed artifacts
                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.findProperty("sonatypeUsername")
                password project.findProperty("sonatypePassword")
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test" || dep.scope == "main"
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'PAI Report Client SDK'
            name 'paireportsclient'
            url 'https://github.com/gopai/paireportsclient'
            organization {
                name 'Payment Alliance International'
                url 'https://gopai.com'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/gopai/paireportsclient/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/gopai/paireportsclient/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/gopai/paireportsclient'
                connection 'scm:git:git:github.com/gopai/paireportsclient.git'
                developerConnection 'scm:git:ssh://git@github.com:gopai/paireportsclient.git'
            }
            developers {
                developer {
                    name 'PAI Developers'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForShadowPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishShadowPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishShadowPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}